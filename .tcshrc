# .tcshrc
umask 027

setenv LS_COLORS di=36
setenv MAIL ~/Maildir/
setenv LESS "-n -s -j4 -R"
setenv CVS_RSH ssh
setenv PAGER "less"
setenv LC_COLLATE C
setenv THREADS_FLAGS native
setenv TEMP /tmp
setenv TMPDIR /tmp

# Command prompt stuff
set prompt="%{\033[36m%}%! %P [%n@%m:%~]%{\033[0m%} %B>%b "
set nobeep
set color
set colorcat
set symlinks=ignore

# Ctrl-D won't log you out
set ignoreeof=3
unset autologout

# Tab completion file listing
set autolist=ambiguous
set complete=igncase
complete cvs p/1/"(add admin annotate checkout commit diff edit \
                   editors export history import init log login \
                   rdiff release remove rtag status tag unedit \
                   update watch)"/

# Command history
set history=2500
set savehist=(2500 merge)
set histlit

# Directory history
alias dirs 'dirs -vl'
set dunique
set pushdsilent
set pushdtohome
alias cd 'pushd \!*'

# Key bindings
#set wordchars='*?_-.[]~=^;${}\'\"\''
bindkey -k up history-search-backward
bindkey -k down history-search-forward
bindkey -a '^[[1;5D' backward-word
bindkey -a '^[[1;5C' forward-word

source ~/.alias

# Source machine-specific configuration
foreach src (`echo ~msoliman/.tcshrc.d/*`)
   source $src
end


